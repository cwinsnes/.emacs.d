* Instructions
This org file dshould be loaded as part of initialization for Emacs 26+.
Use org-babel-load-file to load the file.

Essentially, the file should be loaded from your init.el using
#+BEGIN_EXAMPLE
  (setq straight-use-package-by-default t)
  (defvar bootstrap-version)
  (let ((bootstrap-file
         (expand-file-name "straight/repos/straight.el/bootstrap.el" user-emacs-directory))
        (bootstrap-version 5))
    (unless (file-exists-p bootstrap-file)
      (with-current-buffer
          (url-retrieve-synchronously
           "https://raw.githubusercontent.com/raxod502/straight.el/develop/install.el"
           'silent 'inhibit-cookies)
        (goto-char (point-max))
        (eval-print-last-sexp)))
    (load bootstrap-file nil 'nomessage))

  (org-babel-load-file (expand-file-name "~/.emacs.d/emacsinit.org"))
#+END_EXAMPLE

* Init
** use-package configuration
We are going to be using use-package as a package manager and
package configuration system. This has to be done first in the
file!
#+BEGIN_SRC emacs-lisp
  (straight-use-package 'use-package)
#+END_SRC

Add ability to diminish minor modes from list.
Needs to be done here because it is used by use-package in configurations.
#+BEGIN_SRC emacs-lisp
  (use-package diminish)
#+END_SRC
** OS Specific settings
Make sure that Mac Emacs loads the correct path.
#+begin_src emacs-lisp
  (use-package exec-path-from-shell
    :if (memq window-system '(mac ns))
    :config
    (exec-path-from-shell-initialize)
    ;; The above seems to set the current directory to /
    ;; Reset that to home
    (setq default-directory "~/"))
#+end_src

** General emacs settings and plugins
*** Basic settings
#+BEGIN_SRC emacs-lisp
  ;; Allow for using just y or n instead of writing yes/no.
  (fset 'yes-or-no-p 'y-or-n-p)
  ;; Disable that annoying bell
  (setq ring-bell-function 'ignore)
  ;; Show the time
  (display-time-mode 1)
  (blink-cursor-mode -1)
  ;; Enable emacs server
  (server-start)

  ;; Enable both line and column number in mode-line.
  (line-number-mode t)
  (column-number-mode t)

  ;; Remove a bunch of visual noise from emacs
  (setq inhibit-startup-screen t)
  (menu-bar-mode -1)
  (tool-bar-mode -1)
  (scroll-bar-mode -1)

  ;; Create backup dir instead of having backupfiles everywhere.
  ;; Not sure how much this actually does.
  (let ((autosavedir "~/.emacs.d/saves/")
        (backup-dir "~/.emacs.d/backups/"))
    (dolist (dir (list autosavedir backup-dir))
      (when (not (file-directory-p dir))
        (make-directory dir t)))
    (setq backup-directory-alist `(("." . ,backup-dir))
          auto-save-file-name-transforms `((".*" ,autosavedir t))
          auto-save-list-file-prefix (concat autosavedir ".saves-")
          tramp-backup-directory-alist `((".*" . ,backup-dir))
          tramp-auto-save-directory autosavedir))

  (setq backup-by-copying t
        delete-old-versions t
        kept-new-versions 6
        kept-old-versions 2
        version-control t)

  ;; Make sure that garbage collection is much higher than default
  ;; Important for LSP-mode
  (setq gc-cons-threshold 100000000)
  ;; For the same reason, change read-output-max
  (setq read-process-output-max (* 1024 1024 5))

  ;; Interlocking
  ;; While interlocking is useful, it is extremely rare for this to be
  ;; useful in my usecases and mostly causes annoyance instead.
  (setq create-lockfiles nil)

  ;; Allow extra_files
  (add-to-list 'load-path "~/.emacs.d/extra_files")
#+END_SRC

Remove trailing whitespace upon save
#+BEGIN_SRC emacs-lisp
  (use-package whitespace
    :defer t
    :ensure nil
    :hook (before-save . delete-trailing-whitespace))
#+END_SRC

#+BEGIN_SRC emacs-lisp
  (use-package emacs
    :ensure nil
    :config

    (global-auto-revert-mode)
    (diminish 'auto-revert-mode))
#+END_SRC
*** Understanding Emacs
#+begin_src emacs-lisp
  (use-package which-key
    :config
    (which-key-mode))
#+end_src

*** Programming settings
Try to use space indententation essentially everywere.
#+begin_src emacs-lisp
(add-hook 'find-file-hook '(lambda ()
  (if (and buffer-file-name
           (string-match "/gnulib\\>" (buffer-file-name))
           (not (string-equal mode-name "Change Log"))
           (not (string-equal mode-name "Makefile")))
      (setq indent-tabs-mode nil))))
#+end_src

*** Helm
Enable better minibuffer completions.

#+begin_src emacs-lisp
  (use-package helm
    :defer t
    :config
    (global-set-key (kbd "M-x") #'helm-M-x)
    (global-set-key (kbd "C-x C-f") #'helm-find-files)
    (setq helm-ff-skip-boring-files 1)
    (helm-mode 1)

    (bind-key (kbd "C-w") 'backward-kill-word helm-map)
    (bind-key "<escape>" 'helm-keyboard-quit helm-map))

  (use-package helm-org)
#+end_src
*** Eye-browse
Add a tab-like configuration instead of having to use multiple frames
for different groups of buffers.
#+begin_src emacs-lisp
  (use-package eyebrowse
  :diminish eyebrowse-mode
  :after evil
  :init
  (setq eyebrowse-keymap-prefix (kbd "C-9 C-S-j"))
  :config
  (setq eyebrowse-new-workspace "*scratch*")
  (eyebrowse-mode t)
  ; Most of these are supposed to happen automatically but evil is not
  ; detected for some reason.
  (let ((map eyebrowse-mode-map))
    (define-key map (kbd "C-<") 'eyebrowse-prev-window-config)
    (define-key map (kbd "C->") 'eyebrowse-next-window-config)
    (define-key map (kbd "C-'") 'eyebrowse-last-window-config)
    (define-key map (kbd "C-\"") 'eyebrowse-close-window-config)
    (define-key map (kbd "M-w") 'eyebrowse-close-window-config)
    (define-key map (kbd "M-0") 'eyebrowse-switch-to-window-config-0)
    (define-key map (kbd "M-1") 'eyebrowse-switch-to-window-config-1)
    (define-key map (kbd "M-2") 'eyebrowse-switch-to-window-config-2)
    (define-key map (kbd "M-3") 'eyebrowse-switch-to-window-config-3)
    (define-key map (kbd "M-4") 'eyebrowse-switch-to-window-config-4)
    (define-key map (kbd "M-5") 'eyebrowse-switch-to-window-config-5)
    (define-key map (kbd "M-6") 'eyebrowse-switch-to-window-config-6)
    (define-key map (kbd "M-7") 'eyebrowse-switch-to-window-config-7)
    (define-key map (kbd "M-8") 'eyebrowse-switch-to-window-config-8)
    (define-key map (kbd "M-9") 'eyebrowse-switch-to-window-config-9))
  )
#+end_src
*** Olivetti mode
When writing a single file, it can be nice to have a hyperfocus mode.
Olivett is chosen specifically because it doesn't mess with other
frames, which is nice in combination with EyeBrowse.
#+begin_src emacs-lisp
  (use-package olivetti
    :config
    (setq-default olivetti-body-width 100)
    (setq olivetti-mode-on-hook '()))
#+end_src
*** Graphical configuration
**** Fonts
Make sure all-the-icons is installed.
If the fonts are not already installed, install that as well.
#+BEGIN_SRC emacs-lisp
  (defun aorst/font-installed-p (font-name)
    "Check if font with FONT-NAME is available."
    (if (find-font (font-spec :name font-name))
        t
      nil))

  (use-package all-the-icons
    :config
    (when (and (not (aorst/font-installed-p "all-the-icons"))
               (window-system))
      (all-the-icons-install-fonts t)))
#+END_SRC

#+begin_src emacs-lisp
  (use-package emacs
    :init
    (set-face-attribute 'default nil :font "DejaVu Sans Mono-10" )
    (set-frame-font "Dejavu Sans Mono-10" nil t))
#+end_src

**** Themes
Use Doom emacs themes
#+BEGIN_SRC emacs-lisp
  (use-package doom-themes
    :config
    (setq doom-themes-enable-bold t
          doom-themes-enable-italic t)
    (setq doom-themes-neotree-file-icons t)
    (doom-themes-neotree-config)
    (doom-themes-org-config))
#+END_SRC

***** Theme toggling
The theme toggling code courtesy of Ivan in a [[https://emacs.stackexchange.com/questions/24088/make-a-function-to-toggle-themes][StackOverflow thread]].

Start by setting which themes can be cycled through.
#+begin_src emacs-lisp
  (setq ivan/themes '(doom-one-light doom-gruvbox))
#+end_src

Then define cycling code.
#+begin_src emacs-lisp
  (setq ivan/themes-index 0)

  (defun ivan/cycle-theme (&optional newindex)
    (interactive)
    (when newindex
      (setq ivan/themes-index newindex))
    (setq ivan/themes-index (% (1+ ivan/themes-index) (length ivan/themes)))
    (ivan/load-indexed-theme))

  (defun ivan/load-indexed-theme ()
    (ivan/try-load-theme (nth ivan/themes-index ivan/themes)))

  (defun ivan/try-load-theme (theme)
    (if (ignore-errors (load-theme theme :no-confirm))
        (mapcar #'disable-theme (remove theme custom-enabled-themes))
      (message "Unable to find theme file for ‘%s’" theme)))

  (global-set-key [f10] 'ivan/cycle-theme)
#+end_src

And finally enable the first theme in the list.
#+begin_src emacs-lisp
  (ivan/cycle-theme (- 1 (length ivan/themes)))
#+end_src

**** Look
#+begin_src emacs-lisp
  (use-package doom-modeline
    :init (doom-modeline-mode 1))
#+end_src

#+begin_src emacs-lisp
  (setq display-time-format "%H:%M")
#+end_src

Make delimiters have matched rainbow colors for easier distinguishing.
#+begin_src emacs-lisp
  (use-package rainbow-delimiters
    :config
    (add-hook 'prog-mode-hook #'rainbow-delimiters-mode))
#+end_src

Make identifiers have unique colors in supported modes.
#+begin_src emacs-lisp
  (use-package color-identifiers-mode
    :config
    (add-hook 'after-init-hook 'global-color-identifiers-mode))
#+end_src
**** Cursor tracking
Ways of making sure it is obvious which window is active and where the cursor is.
#+begin_src emacs-lisp
  (use-package beacon
    :config
    (beacon-mode 1))

  (use-package dimmer
    :config
    (dimmer-configure-company-box)
    (dimmer-configure-helm)
    (dimmer-mode t))
#+end_src
** evil-mode - Vim key bindings
Start by unbinding keys that might interfere later.
#+BEGIN_SRC emacs-lisp
  (global-unset-key (kbd "C-w"))
#+END_SRC

Use Vim key bindings by default. Do note that some of the other
packages might set evil mode keys to their commands.
#+BEGIN_SRC emacs-lisp
  (use-package evil
    :diminish evil-mode
    :diminish undo-tree-mode
    :init
    (setq evil-want-C-u-scroll t)
    (setq evil-search-module 'evil-search)
    (setq evil-want-keybinding nil))

  ;; Enable the use of z-<buttons> for folding
  (use-package hideshow
    :config
    (add-hook 'prog-mode-hook 'hs-minor-mode))
#+END_SRC

Redefine =:q= to only close current window while =:quit= exits emacs
entirely. This is mostly important when working with eyebrowse or
similar tabbing items as =:q= by default will kill emacs entirely if
on last window of current tab.
#+begin_src emacs-lisp
  (evil-ex-define-cmd "q" 'delete-window)
  (evil-ex-define-cmd "quit" 'evil-quit)
#+end_src
**** Add key bindings for Evil in different modes

#+begin_src emacs-lisp
    (use-package evil-org
      :ensure t
      :after org
      :config
      (add-hook 'org-mode-hook 'evil-org-mode)
      (add-hook 'evil-org-mode-hook
                (lambda ()
                  (evil-org-set-key-theme)))
      (require 'evil-org-agenda)
      (evil-org-agenda-set-keys))

    (use-package evil-collection
      :after evil
      :config
      (evil-collection-init))

    (use-package evil-magit
      :after magit
      :after evil
      :config
      (evil-ex-define-cmd "GCommit" 'magit-commit)
      (evil-ex-define-cmd "GAdd" 'magit-stage-file))
#+END_SRC
**** Evil Leader key
Add a leader key to evil, and set its bindings.  Yes, this is the same
as simply adding keybindings for all this but I like the ease of
setting this up.
#+BEGIN_SRC emacs-lisp
  ;; Evil leader has to be loaded before Evil to work in initial buffers.
  (use-package evil-leader)

  (define-key evil-normal-state-map (kbd "C-f") 'helm-find-files)
  (evil-leader/set-leader "<SPC>")
  (evil-leader/set-key
    "w" 'save-buffer
    "o" 'delete-other-windows
    "f" 'helm-find-files
    "t" 'counsel-etags-virtual-update-tags
    "g" 'magit-status
    "s" 'default-directory-phi-grep
    "h" 'evil-ex-nohighlight
    "b" 'helm-buffers-list
    "d" 'olivetti-mode)

  (with-eval-after-load 'evil-maps
    (define-key evil-normal-state-map (kbd "C-n") nil)
    (define-key evil-normal-state-map (kbd "C-p") nil))

  (global-evil-leader-mode)
  (evil-mode 1)
#+END_SRC
**** Extra Evil plugins
Add surround, similar to the vim-surround package.
Also define a couple of new surrounds.
#+begin_src emacs-lisp
  (use-package evil-surround
    :diminish
    (evil-surround-mode)
    :config
    (global-evil-surround-mode 1))
#+end_src

** File management
*** Unique buffer names
When working with buffers that have the same name, make emacs display
them using forward style instead of the default post-forward.

#+begin_src emacs-lisp
  (setq-default uniquify-buffer-name-style 'forward
                uniquify-separator "/")

  (setq-default uniquify-after-kill-buffer-p t)
  (setq-default uniquify-ignore-buffers-re "^\\*")
#+end_src
*** Neotree
Add a file browser system to emacs.
While it may not be used much it can be very handy sometimes.
#+BEGIN_SRC emacs-lisp
  (use-package neotree
    :config
    (setq neo-theme (if (display-graphic-p) 'icons 'arrow))
    (define-key evil-normal-state-map [f8] 'neotree-toggle)
    (global-set-key [f8] 'neotree-toggle))
#+END_SRC
*** Magit
[[https://magit.vc/][Magit]], the better interface to git.
#+BEGIN_SRC emacs-lisp
    (use-package magit
      :defer 7)
#+END_SRC
*** Git gutter
#+begin_src emacs-lisp
  (use-package git-gutter
    :diminish git-gutter-mode
    :config
    (global-git-gutter-mode 1))
#+end_src
*** Project management
Use projectile to easily find and search within projects.
#+begin_src emacs-lisp
  (use-package projectile
    :config
    (projectile-mode 1)
    (define-key evil-normal-state-map (kbd "C-p") 'projectile-command-map)
    (define-key projectile-mode-map (kbd "C-c p") 'projectile-command-map))
#+end_src
*** Persistent undo
Allows for using undo between session. Note that saving history to disk risks leaking information.
#+begin_src emacs-lisp
(global-undo-tree-mode)
(setq undo-tree-auto-save-history t)
(setq undo-tree-history-directory-alist '(("." . "~/.emacs.d/undo")))
#+end_src
*** Remote editing
#+begin_src emacs-lisp
  (use-package tramp
    :config
    (add-to-list 'tramp-remote-path "/snap/bin"))
#+end_src
*** Searching
Better grep than the default one.
#+begin_src emacs-lisp
  (use-package phi-grep
    :config
    ;; Note: this one will be very slow for large file trees
    (defun default-directory-phi-grep (regex)
      (interactive
       (list
        (read-regexp (if (fboundp 'read-regexp) "Regexp" "Regexp: "))))
      (phi-grep-in-directory default-directory regex))

    (evil-define-key 'normal 'phi-grep-mode-map (kbd "RET") 'phi-grep-exit)
    (add-to-list 'phi-grep-ignored-files "\.pth")
    (add-to-list 'phi-grep-ignored-files "\.ipynb")
    (add-to-list 'phi-grep-ignored-files "\.tif")
    (add-to-list 'phi-grep-ignored-files "\.odp")
    (add-to-list 'phi-grep-ignored-files "\.odf")
    (add-to-list 'phi-grep-ignored-files "\.odt")
    (add-to-list 'phi-grep-ignored-files "\.ods")
    (add-to-list 'phi-grep-ignored-dirs "__pycache__")
    (add-to-list 'phi-grep-ignored-dirs "\.mypy_cache"))
#+end_src

** File types and languages
*** Org-mode
Set up the basic settings of org-mode.
#+BEGIN_SRC emacs-lisp
  (straight-override-recipe
     '(org :type git :host github :repo "emacsmirror/org" :no-build t))
  (use-package org
      :ensure nil
      :config
      (custom-set-faces
       '(org-headline-done
         ((((class color) (min-colors 16))
           (:foreground "light gray")))))

      (setq org-fontify-done-headline t)
      (setq org-todo-keywords
        '((sequence "TODO(t)" "DOING(o)" "|" "DONE(d)")
          (sequence "MEETING(m)" "|" "DONE(d)")
          (sequence "IDEA" "|")
          (sequence "NOTE" "|")
          (sequence "EVENT(e)" "|")
          (sequence "NEXT" "|")
          (sequence "|" "CANCELED(c)")))
      (setq org-todo-keyword-faces
        '(("TODO" . (:foreground "red" :weight bold))
          ("NEXT" . (:foreground "red" :weight bold))
          ("DOING" . "yellow")
          ("EVENT" . (:foreground "orange" :weight bold))
          ("DONE" . (:foreground "green" :weight bold))
          ("MEETING" . (:foreground "blue" :weight bold))
          ("IDEA" . (:foreground "purple" :weight bold))
          ("NOTE" . (:foreground "purple" :weight bold))
          ("CANCELED" . (:foreground "gray" :weight bold))))
      ;; Make single space end sentences.
      (setq sentence-end-double-space nil)
      (add-hook 'org-mode-hook 'org-indent-mode)
      (eval-after-load 'org-indent '(diminish 'org-indent-mode))
      (setq org-hide-emphasis-markers t)
      (setq org-startup-folded nil)
      (setq org-refile-targets (quote ((nil :maxlevel . 9)
                                       (org-agenda-files :maxlevel . 9))))
      ;; Save after refiling
      (advice-add 'org-refile :after
                  (lambda (&rest _)
                    (org-save-all-org-buffers)))
      (define-key evil-org-mode-map (kbd "C-c C-w") 'org-refile)
      (define-key org-mode-map (kbd "C-c C-w") 'org-refile))


    (require 'org-tempo)
    (setq org-agenda-files '("~/org/"))
#+end_src

**** Org capture
Setup new capture templates.
#+begin_src emacs-lisp
  (setq org-capture-templates
         '(("t" "todo" entry (file org-default-notes-file)
            "* TODO %?\nDEADLINE: %t\n%a\n\n")
           ("m" "Meeting" entry (file org-default-notes-file)
            "* MEETING with %? :MEETING:\n%t")
           ("i" "Idea" entry (file org-default-notes-file)
            "* IDEA %? :IDEA: \n")
           ("e" "Event" entry (file org-default-notes-file)
            "* EVENT %? :EVENT: \n%t\n")
           ("n" "New note" entry (file+headline org-default-notes-file "Quick notes")
            "* NOTE %? \n%t\n")
           ("x" "Next Task" entry (file+headline org-default-notes-file "Tasks")
            "** NEXT %? \nDEADLINE: %t\n") ))

  ;; Make sure that we can use the captures
  (global-set-key (kbd "C-c c") 'org-capture)

  ;; And set the default notes file to be in another location
  ;; Use the index.org from org-wiki to store such things
  (setq org-default-notes-file "~/org/index.org")
#+end_src

**** Org agenda
Add a search system for org tags and such. Use this system to include
any TODO/DOING task and ANY headline with a tag.
#+BEGIN_SRC emacs-lisp
  (use-package org-ql
    :config

    (setq org-agenda-custom-commands
          '(("cav" "Custom Agenda View"
             ((agenda)
              (org-ql-block '(and (not (todo "DONE"))
                                  (or (todo "TODO")
                                      (tags "TODO")
                                      (todo "DOING")
                                      (tags "DOING")
                                      (tags "NEXT")
                                      (tags "todo")))
                            ((org-ql-block-header "To do")))
              (org-ql-block '(and (not (todo "DONE"))
                                  (or (todo "EVENT")
                                      (tags "EVENT")))
                            ((org-ql-block-header "Events")))
              (org-ql-block '(and (not (todo "DONE"))
                                  (or (todo "IDEA")
                                      (tags "IDEA")
                                      (tags "idea")))
                            ((org-ql-block-header "Someday:"))))))))
#+END_SRC

Then use org-super agenda to filter through the results of the search.
#+BEGIN_SRC emacs-lisp
  (use-package org-super-agenda
    :config
    (setq org-super-agenda-groups
          '((:discard (:tag ("ignore")))
            (:name ""
                    :time-grid t)
            (:name "Meetings"
                   :todo "MEETING"
                   :tag "MEETING")
            (:name "TODOs"
                   :time-grid t
                   :todo "TODO"
                   :todo "DOING")
            (:name "IDEAs"
                   :todo "IDEA"
                   :tag "IDEA"
                   :order 9)
            (:name "Results"
                   :tag "results"
                   :tag "result")
            (:auto-tags t)))
    (define-key org-super-agenda-header-map "k" 'org-agenda-previous-line)
    (define-key org-super-agenda-header-map "j" 'org-agenda-next-line)
    (org-super-agenda-mode 1))

  ;; Bind the key to bind together the org-agenda stuff
  (global-set-key (kbd "C-c a") (lambda () (interactive) (org-agenda nil "cav")))
#+END_SRC

**** Org recur
Easier recurring tasks
#+begin_src emacs-lisp
  (use-package org-recur
    :hook ((org-mode . org-recur-mode)
           (org-agenda-mode . org-recur-agenda-mode))
    :demand t
    :config
    (define-key org-recur-mode-map (kbd "C-c d") 'org-recur-finish)

    ;; Rebind the 'd' key in org-agenda (default: `org-agenda-day-view').
    (define-key org-recur-agenda-mode-map (kbd "d") 'org-recur-finish)
    (define-key org-recur-agenda-mode-map (kbd "C-c d") 'org-recur-finish)

    (setq org-recur-finish-done t
          org-recur-finish-archive t))
#+end_src

**** Prettify org
Prettify the way headlines and such look

#+begin_src emacs-lisp
  (use-package org-superstar
    :config
    (setq org-hide-leading-stars nil)
    (setq org-superstar-leading-bullet " ")
    (setq org-superstar-headline-bullets-list '("\u203A"))
    (add-hook 'org-mode-hook 'org-superstar-mode))
#+end_src

**** Org links
Make Org mode open in current window when following links, by default.
Code kinda copied from [[https://stackoverflow.com/questions/17590784/how-to-let-org-mode-open-a-link-like-file-file-org-in-current-window-inste][here]].
#+begin_src emacs-lisp
(defun org-force-open-current-window ()
  (interactive)
  (let ((org-link-frame-setup (quote
                               ((vm . vm-visit-folder)
                                (vm-imap . vm-visit-imap-folder)
                                (gnus . gnus)
                                (file . find-file)
                                (wl . wl)))
                              ))
    (org-open-at-point)))
(define-key org-mode-map (kbd "C-c o") 'org-force-open-current-window)
;; Redefine old behaviour into another mapping
(define-key org-mode-map (kbd "C-c C-o") 'org-open-at-point)
#+end_src

**** Org prettifying
Do not prettify current line.
#+begin_src emacs-lisp
  (defvar my/current-line '(0 . 0)
    "(start . end) of current line in current buffer")
  (make-variable-buffer-local 'my/current-line)

  (defun my/unhide-current-line (limit)
    "Font-lock function"
    (let ((start (max (point) (car my/current-line)))
          (end (min limit (cdr my/current-line))))
      (when (< start end)
        (remove-text-properties start end '(invisible t display "" composition ""))
        (goto-char limit)
        t)))

  (defun my/refontify-on-linemove ()
    "Post-command-hook"
    (let* ((start (line-beginning-position))
           (end (line-beginning-position 2))
           (needs-update (not (equal start (car my/current-line)))))
      (setq my/current-line (cons start end))
      (when needs-update
        (font-lock-fontify-block 2))))

  (defun my/markdown-unhighlight ()
    "Install"
    (font-lock-add-keywords nil '((my/unhide-current-line)) t)
    (add-hook 'post-command-hook #'my/refontify-on-linemove nil t))

  (add-hook 'org-mode-hook #'my/markdown-unhighlight)

  ;; This can be used in markdown down as well.
  ;; markdown-mode is currently not installed and as such is disabled.
  ;;
  ;; (require 'markdown-mode)
  ;; (add-hook 'markdown-mode-hook #'my/markdown-unhighlight)
  ;; (add-hook 'markdown-mode-hook (lambda () (markdown-toggle-markup-hiding 1)))
#+end_src

**** Org-wiki
#+begin_src emacs-lisp
  (load "org-wiki/org-wiki")
  (setq org-wiki-location "~/org")

  (setq org-wiki-template
        (string-trim
  "
  ,#+TITLE: %n
  ,#+DESCRIPTION:
  ,#+KEYWORDS: %n
  ,#+STARTUP:  content
  ,#+DATE: %d

  - [[wiki:index][Index]]

  - Related:

  ,* %n
  "))

  (global-set-key (kbd "C-c n f") 'org-wiki-helm)
  (global-set-key (kbd "C-c n w") 'org-wiki-index)
  (define-key org-mode-map (kbd "C-c n i") 'org-wiki-insert-link)
  (define-key org-mode-map (kbd "C-c n m") 'org-wiki-insert-new)
  (define-key org-mode-map (kbd "C-c n n") 'org-wiki-new)
  (define-key org-mode-map (kbd "C-c n a") 'org-insert-link)
#+end_src

**** Org-transclusion
Add tranclusion ability to org files
#+begin_src emacs-lisp
  (use-package org-transclusion
    :straight (org-transclusion :type git :host github :repo "cwinsnes/org-transclusion")
    :config
    (add-hook 'org-mode-hook 'org-transclusion-mode))
#+end_src

*** Markdown
#+begin_src emacs-lisp
  (setq markdown-fontify-code-blocks-natively t)
#+end_src
*** Python
#+begin_src emacs-lisp
  (setq python-shell-interpreter "python3")
  (if (executable-find "black")
    (use-package blacken
      :hook
      (python-mode . blacken-mode))
    (message "Could not find black"))

  (when (executable-find "ipython")
      (setq python-shell-interpreter "ipython"))

  (if (executable-find "isort")
    (use-package py-isort
      :after python
      :hook ((before-save . py-isort-before-save)))
    (message "Could not find isort"))
#+end_src
*** Rust
Enable rust syntax highlighting and such.
#+begin_src emacs-lisp
  (use-package rust-mode
    :config
    (setq rust-format-on-save t)
    (define-key rust-mode-map (kbd "C-c C-c") 'rust-compile))
#+end_src
*** C
Use irony mode for a better C mode experience.
#+begin_src emacs-lisp
  (use-package irony
    :config
    (add-hook 'c-mode-hook 'irony-mode)
    (add-hook 'irony-mode-hook 'irony-cdb-autosetup-compile-options))

  (use-package flycheck-irony
    :after flycheck
    :config
    (eval-after-load 'flycheck
      '(add-hook 'flycheck-mode-hook #'flycheck-irony-setup)))


  (use-package company-irony
    :after company
    :config
    (eval-after-load 'company
      '(add-to-list 'company-backends 'company-irony)))


  (use-package irony-eldoc
    :config
    (add-hook 'irony-mode-hook #'irony-eldoc))
#+end_src
** General coding plugins
*** Error checking
#+begin_src emacs-lisp
  (use-package flycheck
    :config
    (global-flycheck-mode)
    (add-hook 'python-mode-hook (lambda ()
                                  (setq flycheck-checker 'python-flake8
                                        flycheck-checker-error-threshold 99)))
    (define-key evil-normal-state-map (kbd "M-m") 'flycheck-next-error)
    (define-key evil-normal-state-map (kbd "M-n") 'flycheck-previous-error))
#+end_src
*** Company mode
Completion engine for several languages
#+BEGIN_SRC emacs-lisp
  (use-package company
    :diminish company-mode
    :config
    (add-to-list 'company-backends 'company-capf)
    (add-hook 'after-init-hook 'global-company-mode)
    (setq company-minimum-prefix-length 1
          company-dabbrev-downcase 0
          company-idle-delay 0.0)
    (company-tng-configure-default))
#+END_SRC
*** Snippets
#+begin_src emacs-lisp
  (use-package yasnippet
    :defer 7
    :config

    ;; Expand with yasnippet only if Company didn't complete the word before
    (defvar my-company-point nil)
    (advice-add 'company-complete-common
                :before (lambda () (setq my-company-point (point))))
    (advice-add 'company-complete-common
                :after (lambda ()
                         (when (equal my-company-point (point))
                           (yas-expand))))
    (yas-global-mode 1))

  (use-package yasnippet-snippets
    :after yasnippet)

#+end_src

*** Tags
Make tags work better in Emacs and auto use the correct files.
Will fallback to ripgrep if no tags file is found.
#+begin_src emacs-lisp
  (use-package counsel-etags
    :bind (("C-]" . counsel-etags-find-tag-at-point))
    :init
    ;; Don't ask before rereading the TAGS files if they have changed
    (setq tags-revert-without-query t)
    (setq tags-add-tables nil)
    ;; Don't warn when TAGS files are large
    (setq large-file-warning-threshold nil)
    (add-hook 'prog-mode-hook
              (lambda ()
                (add-hook 'after-save-hook
                          'counsel-etags-virtual-update-tags 'append 'local)))
    :config
    (setq counsel-etags-update-interval 60)
    (add-to-list 'counsel-etags-ignore-directories "build"))

  ;; Bind tag finding to normal key in evil mode
  (with-eval-after-load 'evil-maps
    (define-key evil-normal-state-map (kbd "C-]") 'counsel-etags-find-tag-at-point))
#+end_src

*** LSP-mode
Language server protocol. Works with company-mode!

#+begin_src emacs-lisp
  (use-package lsp-mode
    :hook
    (python-mode . lsp-deferred)
    (rust-mode . lsp-deferred)
    :commands
    (lsp lsp-deferred)
    :config
    (setq lsp-completion-provider :capf
          lsp-diagnostics-provider :flycheck)
    (setq lsp-idle-delay 0.2)
    ;; Python settings
    (setq lsp-pyls-plugins-flake8-enabled t
          lsp-pyls-plugins-pydocstyle-enabled t
          lsp-pyls-plugins-pylint-enabled nil)

    (lsp-register-client
     (make-lsp-client :new-connection (lsp-tramp-connection "pyls")
                      :major-modes '(python-mode)
                      :remote? t
                      :server-id 'pyls-remote)))

  (use-package lsp-ui
    :config
    (setq lsp-ui-doc-position 'bottom))
#+end_src
